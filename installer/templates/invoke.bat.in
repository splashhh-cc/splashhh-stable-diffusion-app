@echo off

PUSHD "%~dp0"
setlocal

call .venv\Scripts\activate.bat
set INVOKEAI_ROOT=.

:start
echo Do you want to generate images using the
echo 1. command-line interface
echo 2. browser-based UI
echo 3. run textual inversion training
echo 4. merge models (diffusers type only)
echo 5. download and install models
echo 6. change InvokeAI startup options
echo 7. re-run the configure script to fix a broken install
echo 8. open the developer console
echo 9. update InvokeAI
echo 10. command-line help
echo Q - quit
set /P restore="Please enter 1-10, Q: [2] "
if not defined restore set restore=2
IF /I "%restore%" == "1" (
    echo Starting the InvokeAI command-line..
    python .venv\Scripts\invokeai.exe %*
) ELSE IF /I "%restore%" == "2" (
    echo Starting the InvokeAI browser-based UI..
    python .venv\Scripts\invokeai.exe --web %*
) ELSE IF /I "%restore%" == "3" (
    echo Starting textual inversion training..
    python .venv\Scripts\invokeai-ti.exe --gui
) ELSE IF /I "%restore%" == "4" (
    echo Starting model merging script..
    python .venv\Scripts\invokeai-merge.exe --gui
) ELSE IF /I "%restore%" == "5" (
    echo Running invokeai-model-install...
    python .venv\Scripts\invokeai-model-install.exe
) ELSE IF /I "%restore%" == "6" (
    echo Running invokeai-configure...
    python .venv\Scripts\invokeai-configure.exe --skip-sd-weight --skip-support-models
) ELSE IF /I "%restore%" == "7" (
    echo Running invokeai-configure...
    python .venv\Scripts\invokeai-configure.exe --yes --default_only
) ELSE IF /I "%restore%" == "8" (
    echo Developer Console
    echo Python command is:
    where python
    echo Python version is:
    python --version
    echo *************************
    echo You are now in the system shell, with the local InvokeAI Python virtual environment activated,
    echo so that you can troubleshoot this InvokeAI installation as necessary.
    echo *************************
    echo *** Type `exit` to quit this shell and deactivate the Python virtual environment ***
    call cmd /k
) ELSE IF /I "%restore%" == "9" (
   echo Running invokeai-update...
   python .venv\Scripts\invokeai-update.exe %*
) ELSE IF /I "%restore%" == "10" (
    echo Displaying command line help...
    python .venv\Scripts\invokeai.exe --help %*
    pause
    exit /b
) ELSE IF /I "%restore%" == "q" (
    echo Goodbye!
    goto ending
) ELSE (
    echo Invalid selection
    pause
    exit /b
)
goto start

endlocal
pause

:ending
exit /b

